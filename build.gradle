import arc.packer.*

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
    }

    ext {
        mainClassName = 'reiyanoigami.Main'

        arcVersion = 'b4dc34071be92f154034d9d9621e015f5c49030c'
        junitVersion = '5.7.0'

        arcModule = { String name ->
            if (name.contains(':')) name = name.split(':')[name.split(':').length - 1]
            return "com.github.Anuken.Arc:$name:$arcVersion"
        }
    }

    dependencies {
        classpath arcModule('arc-core')
        classpath arcModule('extensions:packer')
    }
}

plugins {
    id 'java'
}

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

group 'com.github.nonamecoderssquad'
version '1.0-SNAPSHOT'

dependencies {
    implementation arcModule('arc-core')
    implementation arcModule('backends:backend-sdl')
    implementation arcModule('natives:natives-desktop')

    testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
}

task run(type: JavaExec, dependsOn: classes) {
    mainClass.set project.mainClassName
    classpath sourceSets.main.runtimeClasspath
}

test {
    useJUnitPlatform()
}

task packSprites {
    doLast {
        TexturePacker.process(
                "$rootDir/src/main/resources/sprites-raw",
                "$rootDir/src/main/resources/sprites",
                "sprites.aatls"
        )
    }
}
